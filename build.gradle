buildscript {
    repositories {
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'SpongePowered'
            url = 'http://repo.spongepowered.org/maven'
        }
        jcenter()
    }

    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:${project.forgeGradleVersion}"
        classpath "org.spongepowered:mixingradle:${project.mixinGradleVersion}"
    }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'java'

version = 1.0

sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()

    maven {
        name = 'spongepowered-repo'
        url = 'http://repo.spongepowered.org/maven/'
    }
}

dependencies {
    embed("org.spongepowered:mixin:0.7.11-SNAPSHOT") { transitive = false }
}

minecraft {
    version = project.minecraftVersion
    mappings = project.mappings
    runDir = 'run'
}

processResources {

    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {

        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {

        exclude 'mcmod.info'
    }
}

mixin {
    add sourceSets.main, "mixins.pvp.refmap.json"
}


jar {
    manifest.attributes(
            "MixinConfigs": 'mixins.pvp.json',
            "ForceLoadAsMod": "true",
            "FMLCorePluginContainsFMLMod": 'true',
            "FMLCorePlugin": 'net.pvp.launch.MixinLoader',
            "TweakerClass": "net.pvp.launch.PvPTweaker",
            "TweakOrder": 0,
            "Manifest-Version": 1.0
    )
    dependsOn configurations.compile
    from(configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }) {

        exclude "LICENSE.txt", "META-INF/MANIFSET.MF", "META-INF/maven/**", "META-INF/*.RSA", "META-INF/*.SF", 'META-INF/*.DSA'
    }
}
